group 'org.lice'
version '2.0.0'

buildscript {
	ext.kotlin_version = '1.1.4-2'
	ext.spek_version = '1.1.2'

	repositories {
		mavenCentral()
		// maven{ url 'http://maven.aliyun.com/nexus/content/groups/public/' }
	}
	dependencies {
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
	}

}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'antlr'

sourceCompatibility = 1.6
targetCompatibility = 1.6

repositories {
	mavenCentral()
}

sourceSets {
	main.kotlin.srcDirs += 'src/main/kotlin'
	main.kotlin.srcDirs += 'build/generated-src/antlr/main'
	main.java.srcDirs += 'src/main/java'
	main.java.srcDirs += 'build/generated-src/antlr/main'
}

jar {
	manifest {
		attributes 'Main-Class': 'org.lice.tools.Main'
	}
	from {
		configurations
				.extraLibs
				.collect { it.isDirectory() ? it : zipTree(it) }
	}
}


configurations {
	extraLibs
}

allprojects {
	repositories {
		maven { url 'https://jitpack.io' }
		maven { url "http://dl.bintray.com/jetbrains/spek" }
	}
}

dependencies {
	extraLibs "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
	//extraLibs 'com.github.lice-lang:lice:master-SNAPSHOT'
	extraLibs 'org.jline:jline:3.3.0'
	extraLibs 'org.antlr:antlr4-runtime:4.7'

	configurations.compile.extendsFrom(configurations.extraLibs)

	testCompile group: 'junit', name: 'junit', version: '4.12'
	testCompile "org.jetbrains.spek:spek-api:$spek_version"
	testCompile 'org.junit.platform:junit-platform-runner:1.0.0-RC2'
	testCompile "org.jetbrains.spek:spek-junit-platform-engine:$spek_version"

	antlr "org.antlr:antlr4:4.7" // use ANTLR version 4
}

generateGrammarSource {
	outputDirectory = file("build/generated-src/antlr/main/org/lice/parser")
}